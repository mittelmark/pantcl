---
title: Implementing Pandoc filters using the Tcl programming language
shorttitle: user filters
author:  Detlef Groth
date: 2023-04-17 12:12
---

# Adding your own filters

Since version 0.9.12 it is more easily to add your own filters written in the
Tcl programming language. You can just add in a Markdown file in the
document's YAML header for your code chunk settings a `filter` attribute like this:


```
---
title: "filter-geasy.tcl documentation"
author: "Detlef Groth, Caputh-Schwielowsee, Germany"
date: 2023-04-15
geasy:
     app: graph-easy
     eval: 1
     as: ascii
     filter: user/filter-geasy.tcl
---
``` 

For the actual filter documentation you should read [filter-geasy](filter-geasy.html).

The attribute `filter` should point using either a relative or an absolute path
to your filter implementation. The relative path should be calculated based on
your working directory where you do your document conversion.

How should an actual filter implementation look like? Here an example Tcl
procedure outline:

```
proc filter-CHUNKTYPE {cont dict} {
    ## do the processing
    return [list txt image-path]
}
```

The variable `cont` contains the code chunk code within the triple backticks,
the variable `dict` contains the code chunk arguments as a dictionary (hash)
with key-value pairs for every option given in the code chunk argument. Here
an example:

```
    ```{.chunk key1=true key2="hello"}
    print("Hello!")
    ```
```

So in the example above you would need a procedure name `filter-chunk` the
string `print("Hello")` would be in the variable `cont` and the keys in the dictionary would be `key1` and `key2`.

The returns at the end should be a list with two elements first the text you
would like to display from your filter and secondly an image-path pointing to
a possible image generated by your tool. In case your tool does not generate
images you just return an empty string as the second argument.

Below follows an example for the demonstration of a filter implementation
which supports the [graph-easy](http://bloodgate.com/perl/graph/manual/)
command line application. On a linux system you might simply install this
application using your package manager for instance on my Fedora system I do
this like this:

```
sudo dnf install perl-Graph-Easy
```

Here now the Tcl code with comments:

```{.tcl eval=false}
### file user/filter-geasy.tcl
proc filter-geasy {cont dict} {
    # count all code chunk filters
    global n
    incr n
    # default values for the code chunks attributes
    set def [dict create results show eval true scriptpath scripts \
             app graph-easy label null as ascii ext svg]
    # overwrite them with the current code chunk attributes         
    set dict [dict merge $def $dict]
    # if eval is false return nothing
    if {![dict get $dict eval]} {
        return [list "" ""]
    }
    # check if the application is installed at all
    if {[auto_execok [dict get $dict app]] eq ""} {
        return [list "Error: This filter requires the command line tool [dict get $dict app] - please install it!" ""]
    }
    # intialize return text
    set ret ""
    set owd [pwd]
    # create a filename for the outputs
    if {[dict get $dict label] eq "null"} {
        set fname [file join $owd [dict get $dict scriptpath] geasy-$n]
    } else {
        set fname [file join $owd [dict get $dict scriptpath] [dict get $dict label]]
    }
    if {![file exists [file join $owd [dict get $dict scriptpath]]]} {
        file mkdir [file join $owd [dict get $dict scriptpath]]
    }
    # write down the code chunk text
    set out [open $fname.txt w 0600]
    puts $out $cont
    close $out
    # run the tool
    # TODO: error catching
    set res [exec -ignorestderr [dict get $dict app] --as=[dict get $dict as] $fname.txt]
    # initialize the image variable
    set img ""
    # do the user like to use the dot tool for image generation?
    if {[dict get $dict as] eq "dot"} {
        set out [open $fname.dot w 0600] 
        puts $out $res
        close $out
        if {[auto_execok dot] eq ""} {
             return [list "Error: Dot output requires the command line tool GraphViz dot application - please install GraphViz or add the application sto your PATH variable!" ""]
        }  
        set ext [dict get $dict ext]
        exec -ignorestderr dot $fname.dot -T$ext -o $fname.$ext
        set img $fname.$ext
    }
    # check if results should be shown
    if {[dict get $dict results] eq "show"} {
        set res $res
    } else {
        set res ""
    }
    # return text and image path
    return [list $res $img]
}

```

## See also:

* [filter-geasy](filter-geasy.html) - the actual filter documentation for our example above
* [pantcl Readme](../README.html)
* [pantcl manual](../pantcl.html)
* [pantcl Tutorial](../pantcl-tutorial.html)

